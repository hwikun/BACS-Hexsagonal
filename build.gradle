plugins {
    id 'java'
    id 'java-library'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

var commonProject = project(':common')
var jpaCoreProject = project(jpaCoreModule)

allprojects {
    group = 'com.hwikun'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    ext {
        jjwtVersion = '0.11.5'
        mapStructVersion = '1.5.3.Final'
        mapStructBindingVersion = '0.2.0'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        configureEach {
            // exclude LOGBACK
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
    }

    repositories {
        mavenCentral()
        google()
    }
}

// using project
subprojects {
    dependencies {
        implementation 'org.springframework:spring-web'
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'com.fasterxml.jackson.core:jackson-annotations'
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        // Pageable
        implementation 'org.springframework.data:spring-data-commons'

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    bootJar {
        enabled = false
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
project(':common') {}

project(jpaCoreModule) {
    dependencies {
        implementation commonProject
        api 'org.springframework.boot:spring-boot-starter-data-jpa'
    }
}

project(jasyptCoreModule) {
    dependencies {
        implementation commonProject
        api 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.5'
    }
}

project(mybatisCoreModule) {
    dependencies {
        api 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
        testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.3'
    }
}

project(consulCoreModule) {
    dependencies {
        // Spring Cloud dependency-management
        api platform("org.springframework.cloud:spring-cloud-dependencies:2023.0.0")

        // Consul
        api 'org.springframework.cloud:spring-cloud-starter-consul-discovery'
    }
}

project(exeTimerCoreModule) {
    dependencies {
        implementation commonProject
        implementation 'org.springframework.boot:spring-boot-starter-aop'
    }
}

project(corsCoreModule) {
    dependencies {
        implementation commonProject
        implementation 'org.springframework:spring-webmvc'
    }
}

project(jwtPropertiesModule) {
    dependencies {
        implementation commonProject

        // JWT
        implementation "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
        implementation "io.jsonwebtoken:jjwt-gson:${jjwtVersion}"
    }
}

project(jwtProviderModule) {
    dependencies {
        implementation commonProject
        api project(jwtPropertiesModule)

        // JWT
        implementation "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
        implementation "io.jsonwebtoken:jjwt-gson:${jjwtVersion}"
    }
}

project(secureRandomModule) {
    dependencies {
        implementation commonProject
    }
}

// Authentication ======================================================================================================
